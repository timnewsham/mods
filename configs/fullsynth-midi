#
# A more full-featured synthesizer (than some of the simpler examples).
# This one has:
#    multifunction oscillator
#    oscillator that varies the square-wave duty
#    vibrato
#    portamento
#    dynamics envelope
#    echo delay effect

# these already exist, but we want to change the paramters
D audio audio 6 ""		{y=142; x=781; }
D key key			{y=450; x=24; }
D midi midikey			{y=148; x=24; }

D gen gen			{y=103; x=536; }
D freq freq			{y=28; x=347; }
D env env			{y=201; x=115; }
D port lfo			{y=315; x=231; }
D vibr lfo			{y=29; x=121; }
D echo delay 500		{y=146; x=661; }
D duty lfo			{y=90; x=350; }
D lfoamp const 49		{y=102; x=284; }
D mult mult 32768		{y=196; x=335; }
#D graph oscope outgraph 20000 10 100 {ctrl_y=26; ctrl_x=359; y=190; x=769; }

# these could be shared, but its prettier in the gui like this
D on1 const 1			{y=145; x=233; }
D on2 const 1			{y=365; x=158; }
D on3 const 1			{y=77; x=21; }
D on4 const 1			{y=178; x=458; }
D 50a const 50			{y=120; x=230; }
D 50b const 50			{y=27; x=21; }

# User parameters
D amp knob      Amplitude 0 32767	{y=350; x=17;  ctrl_y=3;   ctrl_x=3;}
D func knob     Function 0 3		{y=203; x=448; ctrl_y=62;  ctrl_x=8;}
D portamp knob  PortDepth 0 16000	{y=324; x=139; ctrl_y=121; ctrl_x=156;}
D portfreq knob PortFreq 0 1000		{y=274; x=147; ctrl_y=62;  ctrl_x=159;}
D portfunc knob PortFunc 0 3		{y=390; x=147; ctrl_y=3;   ctrl_x=158;}
D portduty knob PortDuty 1 99		{y=299; x=149; ctrl_y=180; ctrl_x=160;}
D vibramp knob  VibrDepth 0 300		{y=52;  x=9;   ctrl_y=120; ctrl_x=220;}
D vibrfreq knob VibrFreq 0 1000		{y=2;   x=7;   ctrl_y=61;  ctrl_x=223;}
D vibrfunc knob VibrFunc 0 3		{y=102; x=9;   ctrl_y=2;   ctrl_x=222;}
D lfofunc knob  PhaseFunc 0 2		{y=170; x=233; ctrl_y=4;   ctrl_x=284;}
D lfofreq knob  PhaseFreq 0 1000	{y=77;  x=228; ctrl_y=64;  ctrl_x=286;}
D attack knob   EnvAttack 10 2000	{y=197; x=11;  ctrl_y=2;   ctrl_x=78;}
D decay knob    EnvDecay 5 100		{y=222; x=21;  ctrl_y=61;  ctrl_x=79;}
D sustain knob  EnvSustain 0 100	{y=247; x=13;  ctrl_y=120; ctrl_x=78;}
D release knob  EnvRelease 5 100	{y=273; x=9;   ctrl_y=179; ctrl_x=77;}
D fb knob       EchoFeedback 0 100	{y=213; x=552; ctrl_y=178; ctrl_x=349;}
D delay knob    EchoDelay 50000 500000	{y=186; x=563; ctrl_y=117; ctrl_x=359;}
D emix knob	EchoLevel 0 100		{y=238; x=567; ctrl_y=61;  ctrl_x=362;}




# lfo that alters duty
W lfofreq:val duty:freq
W lfofunc:val duty:func
W on1:out duty:gate
W 50a:out duty:off
W lfoamp:out duty:amp

# lfo that alters volume
W portfreq:val port:freq
W on2:out port:gate
W amp:val port:off
W portamp:val port:amp
W portfunc:val port:func
W portduty:val port:duty

# lfo that alters note pitch
W vibrfreq:val vibr:freq
W on3:out vibr:gate
W midi:note vibr:off
W vibramp:val vibr:amp
W vibrfunc:val vibr:func
W 50b:out vibr:duty

# convert note pitch to frequency
W vibr:out freq:note

# gen envelope
W attack:val env:attack
W decay:val env:decay
W sustain:val env:sustain
W release:val env:release
W midi:vel env:gate

# multiply port amp and envelope to get oscillator amp
W env:out mult:a
W port:out mult:b

# generate a waveform
W duty:out gen:duty
W func:val gen:func
W freq:freq gen:freq
W mult:out gen:amp
W on4:out gen:gate

# add echo
W gen:out echo:in
W delay:val echo:delay
W fb:val echo:feedback
W emix:val echo:mix
W echo:out audio:left
W echo:out audio:right

#W echo:out graph:in

# load up saved knob states
F configs/fullsynth-1

